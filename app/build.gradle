apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'checkstyle'
apply plugin: 'pmd'

/* gets the version name from the latest Git tag */
def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags', '--always'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def isCi = "true" == System.getenv("CI")
def preDexEnabled = "true" == System.getProperty("pre-dex", "true")

def fullApplicationId = "org.greatfire.gfdroid"
def basicApplicationId = "org.greatfire.gfdroid.basic"
// yes, this actually needs both quotes https://stackoverflow.com/a/41391841
def privilegedExtensionApplicationId = '"org.greatfire.gfdroid.privileged"'

def dnsttDomainProp = ""
def dnsttKeyProp = ""
def dnsttPathProp = ""
def dohUrlProp = ""
def dotAddrProp = ""
def defProxyProp = ""

android {

    splits {
        abi {
            enable true
            reset()
            include 'x86', 'armeabi-v7a'
            universalApk true
        }
    }

    if (project.hasProperty("dnsttdomain")) {
        dnsttDomainProp = dnsttdomain
    } else {
        println "WARNING: no dnsttdomain prop found"
    }

    if (project.hasProperty("dnsttkey")) {
        dnsttKeyProp = dnsttkey
    } else {
        println "WARNING: no dnsttkey prop found"
    }

    if (project.hasProperty("dnsttpath")) {
        dnsttPathProp = dnsttpath
    } else {
        println "WARNING: no dnsttpath prop found"
    }

    if (project.hasProperty("dohUrl")) {
        dohUrlProp = dohUrl
    } else {
        println "WARNING: no dohUrl prop found"
    }

    if (project.hasProperty("dotAddr")) {
        dotAddrProp = dotAddr
    } else {
        println "WARNING: no dotAddr prop found"
    }

    if (project.hasProperty("defProxy")) {
        defProxyProp = defProxy
    } else {
        println "WARNING: no defProxy prop found"
    }

    signingConfigs {
        debug {}
        release {}
        'full-gfdroid-release' {}
    }

    compileSdkVersion 30

    defaultConfig {
        versionCode 3
        versionName getVersionName()

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        minSdkVersion 22
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion 25
        /*
          The Android Testing Support Library collects analytics to continuously improve the testing
          experience. More specifically, it uploads a hash of the package name of the application
          under test for each invocation. If you do not wish to upload this data, you can opt-out by
          passing the following argument to the test runner: disableAnalytics "true".
         */
        testInstrumentationRunnerArguments disableAnalytics: 'true'
        vectorDrawables.useSupportLibrary = true

        signingConfig signingConfigs.'full-gfdroid-release'

        buildConfigField "String", "DNSTT_DOMAIN", "\"$dnsttDomainProp\""
        buildConfigField "String", "DNSTT_KEY", "\"$dnsttKeyProp\""
        buildConfigField "String", "DNSTT_PATH", "\"$dnsttPathProp\""
        buildConfigField "String", "DOH_URL", "\"$dohUrlProp\""
        buildConfigField "String", "DOT_ADDR", "\"$dotAddrProp\""
        buildConfigField "String", "DEF_PROXY", "\"$defProxyProp\""
    }

    ext {
        APP_NAME = "@string/app_name"
        APP_NAME_DEBUG = "@string/app_name_debug"
    }

    buildTypes {
        // use proguard on debug too since we have unknowingly broken
        // release builds before.
        all {
            manifestPlaceholders = [ applicationLabel: APP_NAME ]
            minifyEnabled true
            shrinkResources true
            buildConfigField "String", "PRIVILEGED_EXTENSION_PACKAGE_NAME", privilegedExtensionApplicationId
            buildConfigField "String", "ACRA_REPORT_EMAIL", '"reports@f-droid.org"' // String needs both quotes
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', 'src/androidTest/proguard-rules.pro'
        }
        debug {
            manifestPlaceholders = [ applicationLabel: APP_NAME_DEBUG ]
            applicationIdSuffix ".debug"
            resValue "string", "applicationId", fullApplicationId + applicationIdSuffix
            versionNameSuffix "-debug"
            println 'buildTypes.debug defaultConfig.versionCode ' + defaultConfig.versionCode
        }
    }

    flavorDimensions "base", "gfdroid"
    productFlavors {
        full {
            dimension "base"
            applicationId fullApplicationId
            resValue "string", "applicationId", fullApplicationId
        }
        basic {
            dimension "base"
            applicationId basicApplicationId
            resValue "string", "applicationId", basicApplicationId
        }
        gfdroid {
            dimension "gfdroid"
            applicationId basicApplicationId
            resValue "string", "applicationId", fullApplicationId
        }
    }

    compileOptions {
        compileOptions.encoding = "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    aaptOptions {
        cruncherEnabled = false
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            // prevent tests from dying on android.util.Log calls
            returnDefaultValues = true
            all {
                // All the usual Gradle options.
                testLogging {
                    events "skipped", "failed", "standardOut", "standardError"
                    showStandardStreams = true
                }
                systemProperty 'robolectric.dependency.repo.url', 'https://repo1.maven.org/maven2'

                // hack to avoid memory leak crashes
                forkEvery = 1
            }
        }
    }

    sourceSets {
        test {
            java.srcDirs += "$projectDir/src/testShared/java"
        }

        androidTest {
            java.srcDirs += "$projectDir/src/testShared/java"
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError true

        htmlReport true
        xmlReport false
        textReport false

        lintConfig file("lint.xml")
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/INDEX.LIST'
        exclude '.readme'
    }
}

dependencies {
    implementation project(":download")
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.palette:palette:1.0.0'
    implementation 'androidx.work:work-runtime:2.4.0'
    implementation 'com.google.guava:guava:31.0-android' // somehow needed for work-runtime to function

    implementation 'com.google.android.material:material:1.3.0'

    implementation 'com.google.zxing:core:3.3.3'
    implementation 'info.guardianproject.netcipher:netcipher:2.2.0-alpha'
    implementation 'info.guardianproject.panic:panic:1.0'
    implementation 'commons-io:commons-io:2.6'
    implementation 'commons-net:commons-net:3.6'
    implementation 'ch.acra:acra-mail:5.9.0'
    implementation 'ch.acra:acra-dialog:5.9.0'
    implementation 'com.hannesdorfmann:adapterdelegates4:4.3.2'
    implementation "org.slf4j:slf4j-android:1.7.36"

    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
    implementation 'io.reactivex.rxjava3:rxjava:3.0.9'

    implementation 'com.fasterxml.jackson.core:jackson-core:2.11.4'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.11.4'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.11.4'

    // google core classes required by cronet
    implementation 'androidx.core:core:1.6.0'
    implementation 'androidx.core:core-ktx:1.6.0'

    // use local files for debug builds to support development
    debugImplementation files('libs/cronet-debug.aar')
    debugImplementation files('libs/envoy-debug.aar')
    debugImplementation files('libs/IEnvoyProxy.aar')
    // use maven dependencies for release builds to support automation
    releaseImplementation 'org.greatfire.envoy:cronet:102.0.5005.41'
    releaseImplementation 'org.greatfire:envoy:102.0.5005.41'
    releaseImplementation 'org.greatfire:IEnvoyProxy:1.2.0'

    implementation "com.squareup.okhttp3:okhttp:4.9.3"
    implementation "com.squareup.okhttp3:logging-interceptor:4.9.3"

    implementation "io.ktor:ktor-client-core:2.0.0"
    implementation "io.ktor:ktor-client-okhttp:2.0.0"

    implementation "com.github.bumptech.glide:glide:4.12.0"
    annotationProcessor "com.github.bumptech.glide:compiler:4.12.0"

    implementation 'org.bouncycastle:bcprov-jdk15on:1.65'
    fullImplementation 'org.bouncycastle:bcpkix-jdk15on:1.65'
    fullImplementation 'org.jmdns:jmdns:3.5.5'
    fullImplementation 'org.nanohttpd:nanohttpd:2.3.1'

    testImplementation 'androidx.test:core:1.3.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.8.1'
    testImplementation 'org.mockito:mockito-core:3.3.3'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'org.bouncycastle:bcprov-jdk15on:1.65'

    androidTestImplementation 'androidx.arch.core:core-testing:2.1.0'
    androidTestImplementation 'androidx.test:core:1.3.0'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'androidx.test:monitor:1.3.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    androidTestImplementation 'androidx.work:work-testing:2.4.0'
}

checkstyle {
    toolVersion = '7.2'
}

task checkstyle(type: Checkstyle) {
    configFile file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    source 'src/main/java', 'src/test/java', 'src/androidTest/java'
    include '**/*.java'

    classpath = files()
}

pmd {
    toolVersion = '6.20.0'
    consoleOutput = true
}

task pmdMain(type: Pmd) {
    dependsOn 'assembleDebug'
    ruleSetFiles = files("${project.rootDir}/config/pmd/rules.xml", "${project.rootDir}/config/pmd/rules-main.xml")
    ruleSets = [] // otherwise defaults clash with the list in rules.xml
    source 'src/main/java'
    include '**/*.java'
    exclude '**/vendored/**/*.java'
}

task pmdTest(type: Pmd) {
    dependsOn 'assembleDebug'
    ruleSetFiles = files("${project.rootDir}/config/pmd/rules.xml", "${project.rootDir}/config/pmd/rules-test.xml")
    ruleSets = [] // otherwise defaults clash with the list in rules.xml
    source 'src/test/java', 'src/androidTest/java'
    include '**/*.java'
    exclude '**/vendored/**/*.java'
}

task pmd(dependsOn: [pmdMain, pmdTest]) {}
